<?xml version="1.0" encoding="utf-8"?>

<!--

BigBlueButton open source conferencing system - http://www.bigbluebutton.org/

Copyright (c) 2012 BigBlueButton Inc. and by respective authors (see below).

This program is free software; you can redistribute it and/or modify it under the
terms of the GNU Lesser General Public License as published by the Free Software
Foundation; either version 3.0 of the License, or (at your option) any later
version.

BigBlueButton is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.

You should have received a copy of the GNU Lesser General Public License along
with BigBlueButton; if not, see <http://www.gnu.org/licenses/>.

-->

<MDIWindow xmlns="flexlib.mdi.containers.*" 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	width="600" height="400"
	initialize="init()"
	creationComplete="onCreationComplete()"
	implements="org.bigbluebutton.common.IBbbModuleWindow"
	xmlns:mate="http://mate.asfusion.com/"
	title="{ResourceUtil.getInstance().getString('bbb.desktopView.title')}" 
	showCloseButton="false" 
	resize="onResize()">
	
	<mate:Listener type="{ViewStreamEvent.STOP}" method="onStopViewStreamEvent" />
	<mate:Listener type="{CursorEvent.UPDATE_CURSOR_LOC_EVENT}" method="onUpdateCursorEvent" />
	<mate:Listener type="{LocaleChangeEvent.LOCALE_CHANGED}" method="localeChanged" />
		
	<mx:Script>
		<![CDATA[
			import com.asfusion.mate.events.Dispatcher;
			
			import flash.sampler.NewObjectSample;
			
			import flexlib.mdi.events.MDIWindowEvent;
			
			import mx.core.UIComponent;
			
			import org.bigbluebutton.common.Images;
			import org.bigbluebutton.common.LogUtil;
			import org.bigbluebutton.common.events.LocaleChangeEvent;
			import org.bigbluebutton.core.managers.UserManager;
			import org.bigbluebutton.main.views.MainCanvas;
			import org.bigbluebutton.modules.deskshare.events.CursorEvent;
			import org.bigbluebutton.modules.deskshare.events.StartedViewingEvent;
			import org.bigbluebutton.modules.deskshare.events.ViewStreamEvent;
			import org.bigbluebutton.modules.deskshare.events.ViewWindowEvent;
			import org.bigbluebutton.modules.deskshare.model.DeskshareOptions;
			import org.bigbluebutton.util.i18n.ResourceUtil;

			private var screenHeight:Number = Capabilities.screenResolutionY;
			private var screenWidth:Number = Capabilities.screenResolutionX;
						
			private var images:Images = new Images();
			[Bindable] public var fitToWidthIcon:Class = images.magnifier;
			[Bindable] public var fitToActualSizeIcon:Class = images.mag_reset;
			
			private var cursor:Shape;
			
			private var video:Video;
			private var ns:NetStream;
			private var videoHolder:UIComponent = new UIComponent();
			private var stream:String;
			private var videoHeight:Number;
			private var videoWidth:Number;
			
			private static const VIDEO_WIDTH_PADDING:int = 7;
			private static const VIDEO_HEIGHT_PADDING:int = 65;

			// The following code block is to deal with a bug in FLexLib 
			// with MDI windows not responding well to being maximized
			private var savedWindowWidth:Number;
			private var savedWindowHeight:Number;
			private var savedX:Number;
			private var savedY:Number;
			private var isMaximized:Boolean = false;
			
			[Bindable] private var baseIndex:int;
			[Bindable] private var dsOptions:DeskshareOptions;
									
			private function init():void{
				dsOptions = new DeskshareOptions();
				baseIndex = dsOptions.baseTabIndex;
			}
									
			private function onCreationComplete():void{
				videoHolder.addChild(video);				
				this.addChild(videoHolder);
				videoHolder.percentWidth = 100;
				videoHolder.percentHeight = 100;
				this.addEventListener(MDIWindowEvent.RESIZE, onResize);					
				cursor = new Shape();
				cursor.graphics.lineStyle(6, 0xFF0000, 0.6);
				cursor.graphics.drawCircle(0,0,3);
				videoHolder.addChild(cursor);
				videoHolder.addChild(cursorImg);
				cursor.visible = false;
				
				resourcesChanged();
				
				titleBarOverlay.tabIndex = baseIndex;
				minimizeBtn.tabIndex = baseIndex+1;
				maximizeRestoreBtn.tabIndex = baseIndex+2;
				closeBtn.tabIndex = baseIndex+3;
			}
			
			private function onUpdateCursorEvent(event:CursorEvent):void {	
				cursor.x = ((event.x/videoWidth)) * videoHolder.width;
				cursor.y = ((event.y/videoHeight)) * videoHolder.height;
			
				cursorImg.visible = true;
                // DO NOT compute the x and y coordinate and assign directly to the cursorImg
                // as it results in a flickering and jerky mouse pointer (ralam jun 10, 2010).
                if (cursor.x < videoHolder.x) cursor.x = videoHolder.x;
                if (cursor.y < videoHolder.y) cursor.y = videoHolder.y;
                if (cursor.x > videoHolder.x + videoHolder.width) cursor.x = videoHolder.x + videoHolder.width;
                if (cursor.y > videoHolder.y + videoHolder.height) cursor.y = videoHolder.y + videoHolder.height;
                cursorImg.x = cursor.x;
                cursorImg.y = cursor.y;
			}
			
			public function startVideo(connection:NetConnection, stream:String, width:Number, height:Number):void{
				this.videoWidth = width;
				this.videoHeight = height;
				
				ns = new NetStream(connection);
				ns.addEventListener( NetStatusEvent.NET_STATUS, onNetStatus );
				ns.addEventListener(AsyncErrorEvent.ASYNC_ERROR, onAsyncError);
				ns.client = this;
				ns.bufferTime = 0;
				ns.receiveVideo(true);
				ns.receiveAudio(false);
				
				video = new Video(width, height);
				video.width = width;
				video.height = height;
				video.attachNetStream(ns);
				ns.play(stream);	
				this.stream = stream;
				this.title = "Viewing Remote Desktop";
				onResize();
			}
			
			public function stopViewing():void {
				ns.close();
				closeWindow();				
			}
			
			private function onStopViewStreamEvent(event:ViewStreamEvent):void {
				stopViewing();
			}
						
			private function onAsyncError(e:AsyncErrorEvent):void{
				LogUtil.debug("VIdeoWindow::asyncerror " + e.toString());
			}
			
			private function onNetStatus(e:NetStatusEvent):void{
				switch(e.info.code){
				case "NetStream.Play.Start":
					LogUtil.debug("NetStream.Publish.Start for broadcast stream " + stream);
									LogUtil.debug("Dispatching start viewing event");
				var dispatcher: Dispatcher  = new Dispatcher();
				dispatcher.dispatchEvent(new StartedViewingEvent(StartedViewingEvent.STARTED_VIEWING_EVENT));
					break;
				case "NetStream.Play.UnpublishNotify":
					LogUtil.debug("NetStream.Play.UnpublishNotify for broadcast stream " + stream);
					stopViewing();
					break;
				}
			}
			
			public function getPrefferedPosition():String{
				return MainCanvas.DESKTOP_SHARING_VIEW;
			}
			
			private function onResize(e:MDIWindowEvent = null):void {
				if (btnActualSize == null) return;
				// resize
				if (btnActualSize.selected) {
					videoHolder.width = video.width = videoWidth;
					videoHolder.height = video.height = videoHeight;
				} else {
					var maxWidth:Number = this.width - VIDEO_WIDTH_PADDING; // Max width for the image
					var maxHeight:Number = this.height - VIDEO_HEIGHT_PADDING;    // Max height for the image
					var ratio:Number = 0;  // Used for aspect ratio
					var origWidth:Number = videoWidth;    // Current image width
					var origHeight:Number = videoHeight;  // Current image height
					
					if (origWidth > maxWidth && origHeight > maxHeight) {
						ratio = Math.min(maxWidth/origWidth, maxHeight/origHeight);
						videoHolder.width = origWidth * ratio;
						videoHolder.height = origHeight * ratio;
					} else if (origWidth > maxWidth) {
						ratio = maxWidth/origWidth;
						videoHolder.width = origWidth * ratio;
						videoHolder.height = origHeight * ratio;
					} else if (origHeight > maxHeight) {
						ratio = maxHeight/origHeight;
						videoHolder.width = origWidth * ratio;
						videoHolder.height = origHeight * ratio;
					} else {
						ratio = Math.min(maxWidth/origWidth, maxHeight/origHeight);
						videoHolder.width = origWidth * ratio;
						videoHolder.height = origHeight * ratio;
					}
					
					video.width = videoHolder.width;
					video.height = videoHolder.height;
				}
				
				// center
				if (videoHolder.width < this.width - VIDEO_WIDTH_PADDING)
					video.x = (this.width - VIDEO_WIDTH_PADDING - videoHolder.width) / 2;
				else
					video.x = 0;
				if (videoHolder.height < this.height - VIDEO_HEIGHT_PADDING)
					video.y = (this.height - VIDEO_HEIGHT_PADDING - videoHolder.height) / 2;
				else 
					video.y = 0;
			}
			
			private function closeWindow():void {
				dispatchEvent(new ViewWindowEvent(ViewWindowEvent.CLOSE));
			}
			
			override protected function resourcesChanged():void{
				super.resourcesChanged();
				this.title = ResourceUtil.getInstance().getString('bbb.desktopView.title');
				
				if (windowControls != null) {
					minimizeBtn.toolTip = ResourceUtil.getInstance().getString("bbb.window.minimizeBtn.toolTip");
					minimizeBtn.accessibilityName = ResourceUtil.getInstance().getString("bbb.desktopView.minimizeBtn.accessibilityName");

					maximizeRestoreBtn.toolTip = ResourceUtil.getInstance().getString("bbb.window.maximizeRestoreBtn.toolTip");
					maximizeRestoreBtn.accessibilityName = ResourceUtil.getInstance().getString("bbb.desktopView.maximizeRestoreBtn.accessibilityName");

					closeBtn.toolTip = ResourceUtil.getInstance().getString("bbb.window.closeBtn.toolTip");
					closeBtn.accessibilityName = ResourceUtil.getInstance().getString("bbb.desktopView.closeBtn.accessibilityName");
				}
			}
			
			private function localeChanged(e:LocaleChangeEvent):void{
				resourcesChanged();
			}
			
			private function toggleFullscreen():void {
				if (stage.displayState == StageDisplayState.NORMAL) {
					stage.displayState = StageDisplayState.FULL_SCREEN;
					maximize();
				} else
					stage.displayState = StageDisplayState.NORMAL;
			}
			
		]]>
	</mx:Script>
	<mx:Move id="cursorMove" target="{cursorImg}"/>
	<mx:Image id="cursorImg" visible="false" source="@Embed('../../assets/images/cursor4.png')"/>
		
	<mx:ControlBar id="bottomBar" height="35" horizontalAlign="center">
		<mx:Button id="btnActualSize" 
				   toggle="true" 
				   click="onResize()" 
				   selected="false"
				   height="90%" 
				   label="{btnActualSize.selected ? ResourceUtil.getInstance().getString('bbb.desktopView.fitToWindow') : ResourceUtil.getInstance().getString('bbb.desktopView.actualSize')}" 
				   toolTip="{btnActualSize.selected ? ResourceUtil.getInstance().getString('bbb.desktopView.fitToWindow') : ResourceUtil.getInstance().getString('bbb.desktopView.actualSize')}" 
				   tabIndex="{baseIndex+4}"/>
		<mx:Button id="toggleFullscreenBtn" click="toggleFullscreen()" label="Fullscreen" tabIndex="{baseIndex+5}" />
	</mx:ControlBar>
</MDIWindow>
